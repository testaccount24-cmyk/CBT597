//MPFCMDS   JOB NOTIFY=&SYSUID.
//*------------------------------------------------------------------*
//* BEFORE SUBMITTING
//* CHANGE "YOUR.LINKLIB" ---> YOUR LINKLIB
//*------------------------------------------------------------------*
//ASM      EXEC PGM=ASMA90,
//             PARM='OBJECT,NOTEST,RENT,NOALIGN'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(10,5))
//SYSIN    DD  *
MPFCMDS  TITLE 'MPF EXIT TO ISSUE START COMMAND BASED ON MESSAGE ID.'
*
MPFCMDS  CSECT
MPFCMDS  AMODE 31
MPFCMDS  RMODE 24
         BAKR   R14,0               INITIALISATION
         LR    R12,R15
         USING MPFCMDS,R12
         L     R10,0(R1)           POINT TO CTXT PARAMETER BLOCK
         USING CTXT,R10            ESTABLISH ADDRESSABILITY
         L     R9,CTXTTXPJ         POINT TO MESSAGE ATTRIBUTES
         USING CTXTATTR,R9         ESTABLISH ADDRESSABILITY
         LA    R0,WORKSIZE
*
*  INITIALISE WORKAREA
*
         GETMAIN RU,LV=(R0),LOC=BELOW     GET SOME WORKING STORAGE
         LR    R2,R1               POINT TO REENTRANT WORK AREA
         LA    R3,PSETSIZE         STORE LENGTH OF PRESET CONSTANTS
         LR    R5,R3               COPY TO R4 FOR MVCL
         LA    R4,PSETAREA         POINT TO PRESET DATA AREA
         MVCL  R2,R4               PRIME WORK AREA WITH PRESET DATA
         LR    R5,R1               SAVE ADDRESS OF GETMAINED STORAGE
         USING WORKAREA,R5         ESTABLISH ADDRESSABILITY
         EJECT
*
*  DETERMINE CASE TO PROCESS
*
MPFXT100 EQU   *
         LA    R2,CTXTTMSG         CALCULATE LENGTH OF MSGID
         LA    R3,CTXTTMSG+8
MPFXT105 CLI   0(R2),C' '          MESSAGE ID IS DELIMITED BY BLANK
         BE    MPFXT110
         CR    R2,R3
         BE    MPFXT110            REACHED THE MAX LENGTH
         LA    R2,1(,R2)
         B     MPFXT105
MPFXT110 LA    R3,CTXTTMSG         @ START OF MSGID
         SR    R2,R3               R2=L'(MSGID)-1
         BCTR  R2,0                DECREMENT FOR EXECUTED MOVE
         EX    R2,MVMBR            MOVE MEMBER NAME TO DYNALLOC TU
*
*  COMMAND ISSUE ROUTINE
*
         XR    R0,R0               CLEAR FOR MGCR/SVC 34
         MGCR  CMDHDR              POINT TO COMMAND
*
*     END OF COMMANDS - TIDY UP AND GO HOME
*
QUIT     CNOP  0,4
         LA    R2,WORKAREA
         LA    R1,WORKSIZE
         FREEMAIN R,LV=(R1),A=(R2)
         EJECT
*
*     END OF PROCESSING.
*
         PR
MVMBR    MVC  CMDSMBR(0),CTXTTMSG  MOVE MESSAGE ID TO MEMBER NAME
         EJECT
*
*        CONSTANT DATA.
*
         SPACE 2
         LTORG
         EJECT
*
*     VARIABLE DATA. NOTE THAT THIS DATA IS COPIED TO A WORK AREA
*     AND UPDATED THERE.
*
         SPACE 2
PSETAREA DS    0D                  PRESET MGCR WORK AREA
SSCMD    DC    AL2(SCMDLN)
         DC    XL2'00'
         DC    CL17'S MPFCMDS,MEMBER='
*                   12345678901234567
         DC    CL8'        '
         DC    C'  /* ISSUED BY MPFCMDS */ '
SCMDLN   EQU   *-SSCMD
PSETSIZE EQU   *-PSETAREA          SIZE OF THE VARIABLE WORK AREA
         EJECT
*
*     MAP PARAMETER LIST TO MPFCMDS.
*
         EJECT
WORKAREA DSECT                     PRESET WORK AREA
         DS    0D                  PRESET WORK AREA
CMDHDR   DC    XL2'20'
         DC    XL2'00'
CMDTEXT  DC    CL17' '
CMDSMBR  DC    CL8'        '                  COMMAND FILE MEMBER
         DC    C'  /* ISSUED BY MPFCMDS */ '
WORKSIZE EQU   *-WORKAREA          SIZE OF THE VARIABLE WORK AREA
         IEZVX100
*
*     REGISTER EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
//SYSLIN   DD  DSN=&&LOADME,DISP=(,PASS),UNIT=SYSDA,
//             SPACE=(TRK,(30,10),RLSE)
//SYSPRINT DD  SYSOUT=*
//LKED     EXEC PGM=HEWL,PARM='RENT,REUS,XREF',
//             COND=(8,LT,ASM)
//SYSLMOD  DD  DSN=YOUR.LINKLIB,DISP=SHR
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(3,2)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&LOADME,DISP=(OLD,DELETE)
//         DD *
 ENTRY MPFCMDS
 NAME MPFCMDS(R)
